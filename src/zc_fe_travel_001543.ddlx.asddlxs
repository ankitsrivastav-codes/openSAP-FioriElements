@Metadata.layer: #CORE
@UI.headerInfo: { typeName: 'Travel',
                  typeNamePlural: 'Travels',
                  title: { type: #STANDARD, value: 'Description' },
                  description: { type: #STANDARD, value: 'TravelID' } }
annotate entity ZC_FE_Travel_001543 with
{
  @UI.facet: [{ purpose: #HEADER, type: #DATAPOINT_REFERENCE, position: 10, targetQualifier: 'PriceData' },
              { purpose: #HEADER, type: #DATAPOINT_REFERENCE, position: 20, targetQualifier: 'StatusData' },
              { purpose: #STANDARD, type: #COLLECTION, position: 10, id: 'GeneralInfo', label: 'General Information' },
              { purpose: #STANDARD, type: #IDENTIFICATION_REFERENCE, position: 10, parentId: 'GeneralInfo', label: 'General' },
              { purpose: #STANDARD, type: #FIELDGROUP_REFERENCE, position: 20, parentId: 'GeneralInfo', label: 'Prices', targetQualifier: 'PriceGroup' },
              { purpose: #STANDARD, type: #FIELDGROUP_REFERENCE, position: 30, parentId: 'GeneralInfo', label: 'Dates', targetQualifier: 'DateGroup' },
              { purpose: #STANDARD, type: #LINEITEM_REFERENCE, position: 20, id: 'Booking', label: 'Bookings', targetElement: '_Booking' }]

  @UI.identification: [{ position: 10 }]
  Description;
  @UI.lineItem: [{ position: 10 }]
  TravelID;
  @UI.lineItem: [{ position: 30 }]
  @UI.selectionField: [{position: 10 }]
  @UI.identification: [{ position: 20 }]
  AgencyID;
  @UI.lineItem: [{ position: 40 }]
  @UI.selectionField: [{position: 20 }]
  @UI.identification: [{ position: 30 }]
  CustomerID;
  @UI.lineItem: [{ position: 50 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'DateGroup' }]
  BeginDate;
  @UI.lineItem: [{ position: 60 }]
  @UI.fieldGroup: [{ position: 20, qualifier: 'DateGroup' }]
  EndDate;
  @UI.lineItem: [{ position: 70 }]
  @UI.fieldGroup: [{ position: 10, qualifier: 'PriceGroup' }]
  BookingFee;
  @UI.lineItem: [{ position: 80 }]
  @UI.dataPoint: { qualifier: 'PriceData', title: 'Price Data' }
  @UI.fieldGroup: [{ position: 20, qualifier: 'PriceGroup' }]
  TotalPrice;
  @UI.lineItem: [{ position: 90, criticality: 'OverallStatusCriticality' }]
  @UI.dataPoint: { qualifier: 'StatusData', title: 'Status Data', criticality: 'OverallStatusCriticality' }
  @UI.selectionField: [{position: 50 }]
  @UI.textArrangement: #TEXT_ONLY
  OverallStatus;
  @UI.lineItem: [{ position: 100 }]
  LocalLastChangedAt;
}